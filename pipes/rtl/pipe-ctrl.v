  
//========================================================================
// pipes/rtl/pipe-ctrl.v
//========================================================================
// Sebastian Claudiusz Magierowski Apr 16 2026
/*
  1. Wait idle with ctrl_src_rdy_o = 1
  2. When ctrl_src_val_i && ctrl_src_rdy_o, accept the command
  3. Move into “response pending” state
  4. Drive ctrl_snk_val_o = 1 and ctrl_snk_msg_o = 32'd1
  5. When ctrl_snk_val_o && ctrl_snk_rdy_i, clear the pending response and return to idle
*/

`ifndef PIPE_CTRL_V
`define PIPE_CTRL_V

`include "vc-trace.v"

module pipe_ctrl
(
  input  logic        clk,
  input  logic        reset,

  input  logic        ctrl_src_val_i,
  output logic        ctrl_src_rdy_o,
  input  logic [31:0] ctrl_src_msg_i,

  output logic        ctrl_snk_val_o,
  input  logic        ctrl_snk_rdy_i,
  output logic [31:0] ctrl_snk_msg_o
);

  logic        resp_pending;
  logic [31:0] last_cmd;

  assign ctrl_src_rdy_o = !resp_pending;
  assign ctrl_snk_val_o = resp_pending;
  assign ctrl_snk_msg_o = 32'd1;

  always @( posedge clk ) begin
    if ( reset ) begin
      resp_pending <= 1'b0;
      last_cmd     <= 32'b0;
    end
    else begin
      if ( ctrl_src_val_i && ctrl_src_rdy_o ) begin
        resp_pending <= 1'b1;
        last_cmd     <= ctrl_src_msg_i;
      end
      else if ( ctrl_snk_val_o && ctrl_snk_rdy_i ) begin
        resp_pending <= 1'b0;
      end
    end
  end

  task trace
  (
    inout [`VC_TRACE_NBITS-1:0] trace_str
  );
  reg [39:0] state_str;
  begin
    if ( ctrl_src_val_i && ctrl_src_rdy_o ) begin
      $sformat( state_str, "a:%x", ctrl_src_msg_i[15:0] );
      vc_trace.append_str( trace_str, state_str );
    end
    else if ( ctrl_snk_val_o ) begin
      vc_trace.append_str( trace_str, "done" );
    end
    else begin
      $sformat( state_str, "i:%x", last_cmd[15:0] );
      vc_trace.append_str( trace_str, state_str );
    end
  end
  endtask

endmodule

`endif /* PIPE_CTRL_V */
