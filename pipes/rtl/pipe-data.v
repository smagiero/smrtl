//========================================================================
// pipes/rtl/pipe-data.v
//========================================================================
// Sebastian Claudiusz Magierowski Apr 17 2026

`ifndef PIPE_DATA_V
`define PIPE_DATA_V

`include "pipe-stage.v"
`include "vc-trace.v"

module pipe_data
#(
  parameter p_num_stages = 2
)(
  input  logic        clk,
  input  logic        reset,

  input  logic        pipe_start_i,
  input  logic [31:0] num_inputs_i,
  output logic        pipe_done_o,

  input  logic        data_src_val_i,
  output logic        data_src_rdy_o,
  input  logic [63:0] data_src_msg_i,

  output logic        data_snk_val_o,
  input  logic        data_snk_rdy_i,
  output logic [63:0] data_snk_msg_o
);

  localparam [31:0] c_num_stages = p_num_stages;

  logic        running_reg; // is pipe running (i.e., has it been started and not yet completed)?
  logic [31:0] retired_outputs_reg; // how many outputs succesfully pumpd out of pipe
  logic        data_go;
  logic [63:0] stage_msg [0:p_num_stages];
  logic        stage_val [0:p_num_stages];
  logic        stage_rdy [0:p_num_stages];

  assign stage_val[0]   = running_reg && data_src_val_i;
  assign stage_msg[0]   = data_src_msg_i;
  assign data_go        = data_snk_val_o && data_snk_rdy_i;
  assign data_src_rdy_o = running_reg && stage_rdy[0];
  assign data_snk_val_o = stage_val[p_num_stages];
  assign data_snk_msg_o = stage_msg[p_num_stages];
  assign pipe_done_o    = running_reg && data_go && ( retired_outputs_reg + 32'd1 == num_inputs_i );
  assign stage_rdy[p_num_stages] = data_snk_rdy_i;

  genvar s;
  generate
    for ( s = 0; s < p_num_stages; s = s + 1 ) begin : STAGE
      pipe_stage#(
        .p_data_nbits ( 64    ),
        .p_addend     ( 64'd1 )
      )
      stage
      (
        .clk       ( clk           ),
        .reset     ( reset         ),
        .in_val_i  ( stage_val[s]  ),
        .in_rdy_o  ( stage_rdy[s]  ),
        .in_msg_i  ( stage_msg[s]  ),
        .out_val_o ( stage_val[s+1] ),
        .out_rdy_i ( stage_rdy[s+1] ),
        .out_msg_o ( stage_msg[s+1] )
      );
    end
  endgenerate

  always @( posedge clk ) begin
    if ( reset ) begin
      running_reg         <= 1'b0;
      retired_outputs_reg <= 32'b0;
    end
    else begin
      if ( pipe_start_i ) begin
        running_reg         <= 1'b1;
        retired_outputs_reg <= 32'b0;
      end
      else if ( pipe_done_o ) begin
        running_reg         <= 1'b0;
        retired_outputs_reg <= retired_outputs_reg + 32'd1;
      end
      else if ( data_go ) begin
        retired_outputs_reg <= retired_outputs_reg + 32'd1;
      end
    end
  end

  //----------------------------------------------------------------------
  // Line Tracing
  //----------------------------------------------------------------------

  logic [95:0] state_str;

  `VC_TRACE_BEGIN
  begin
    $sformat( state_str, "r:%x/%x n:%x ", retired_outputs_reg[7:0], num_inputs_i[7:0], c_num_stages[7:0] );
    vc_trace.append_str( trace_str, state_str );
    if ( p_num_stages > 0 ) begin
      STAGE[0].stage.trace( trace_str );
    end
    if ( p_num_stages > 1 ) begin
      vc_trace.append_str( trace_str, "|" );
      STAGE[1].stage.trace( trace_str );
    end
    if ( p_num_stages > 2 ) begin
      vc_trace.append_str( trace_str, "|" );
      STAGE[2].stage.trace( trace_str );
    end
    if ( p_num_stages > 3 ) begin
      vc_trace.append_str( trace_str, "|" );
      STAGE[3].stage.trace( trace_str );
    end
  end
  `VC_TRACE_END

endmodule

`endif /* PIPE_DATA_V */
